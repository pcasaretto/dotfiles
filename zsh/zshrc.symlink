# shortcut to this dotfiles path is $DOTFILES
DOTFILES=$HOME/.dotfiles
WORKSPACE=$HOME/workspace

LANG='en_US.UTF-8'

# all of our zsh files
typeset -u config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load the theme
source "$DOTFILES/zsh/themes/gallois.zsh-theme"

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
